;=====================================================================
; product specific variables CLAWS-MAIL
;
; J. Boettge <boettge@mpi-halle.mpg.de>  2018-07-30 10:43:05 +0200
;=====================================================================

include_insert "%ScriptPath%\variables.opsiinc"

Set $MAP$ 				= getProductMap
Set $FullProductId$   	= getValue("id", $MAP$)
Set $ProductName$     	= getValue("name", $MAP$)
Set $Version$         	= getValue("ProductVersion", $MAP$)
Set $SystemType$      	= GetSystemType
Set $NTVersion$   		= GetMsVersionInfo

;-----------------------------------------------------------
;*** basic requirements
;-----------------------------------------------------------
requiredWinstVersion >= "4.11.6"

if CompareDotSeparatedNumbers($NTVersion$, "<", "6.1")
	LogError "OS is not Windows 7 or above"
	isFatalError
endif

if not ($SystemType$ = "64 Bit System")
	; LogError "OS is not a 64 Bit System"
	; isFatalError

	LogWarning "OS is not a 64 Bit System; 32 Bit is not explicitly supported"
endif

;-----------------------------------------------------------
;*** get LogLevel from property
Set $LogLevelDefault$ = GetProductProperty("log_level","default")
;-----------------------------------------------------------
;*** detect testing package:
if (strPos($FullProductId$, "0_") = "0") and (strPos($FullProductId$, "test_") = "0")
	; not a testing package
	set $ProductId$  	= $FullProductId$ 
	set $Testing$ 		= "false"
	if ($LogLevelDefault$ = "default")
		set $LogLevelDefault$  	= "{{DEFAULT_RELEASE_LOGLEVEL}}"
	endif
else
	; this is a testing package
	comment "Prefix for testing package detected"
	set $ProductId$  		= strPart($FullProductId$, calculate(strPos($FullProductId$,"_") + "+1"), strLength($FullProductId$))
	set $Testing$ 		= "true"
	if ($LogLevelDefault$ = "default")
		set $LogLevelDefault$  	= "{{DEFAULT_TESTING_LOGLEVEL}}"
	endif
endif
setlogLevel = $LogLevelDefault$

;-----------------------------------------------------------
;*** detect DFN package:
;-----------------------------------------------------------
if strPos($FullProductId$, "dfn_") = "0"
	set $isDFN$ = "false"
	set $StrippedProductId$ = $ProductId$
else
	set $isDFN$ = "true"
	set $StrippedProductId$ = strPart($ProductId$,"5",strLength($ProductId$))
endif

;-----------------------------------------------------------
;*** Images
;-----------------------------------------------------------
if ($isDFN$ = "true")
	set $Img_Install$		= "%ScriptPath%\images\" + $StrippedProductId$ + "_install_dfn.png"
else
	set $Img_Install$		= "%ScriptPath%\images\" + $StrippedProductId$ + "_install.png"
endif
set $Img_Install_Custom$	= "%ScriptPath%\images\" + $StrippedProductId$ + "_install_custom.png"
Set $Img_Uninstall$			= "%ScriptPath%\images\" + $StrippedProductId$ + "_uninstall.png"
Set $Img_Uninstall_Custom$	= "%ScriptPath%\images\" + $StrippedProductId$ + "_uninstall_custom.png"
Set $Img_Prepare$			= "%ScriptPath%\images\" + $StrippedProductId$ + "_prepare.png"
Set $Img_Plugins$			= "%ScriptPath%\images\" + $StrippedProductId$ + "_plugins.png"
Set $Img_Update$			= "%ScriptPath%\images\" + $StrippedProductId$ + "_update.png"

comment "preparing..."
ShowBitmap $Img_Prepare$ $ProductName$

;-----------------------------------------------------------
;*** software architecture; valid values: "32 bit", "64 bit", "sysnative"
;-----------------------------------------------------------
; make the systemtype shorter:
if ($SystemType$ = "64 Bit System")
	set $SystemType$ ="x64"
else
	set $SystemType$ ="x32"
endif

Set $sw_arch$ = GetProductProperty("install_architecture","sysnative")
Set $sw_arch$ = takeString(0, splitString($sw_arch$, " "))
; Set $sw_arch$		  = "32"

if ( $sw_arch$ = "sysnative" )
	if ($SystemType$ = "x64")
		Set $sw_arch$ = "64"
	else
		Set $sw_arch$ = "32"
	endif
endif

if ($sw_arch$ = "64")
	; 64 bit software
	set $InstallBase$ = "%ProgramFiles64Dir%"
else
	; 32 bit software
	set $InstallBase$ = EnvVar("ProgramFiles(x86)")
endif

if ($SystemType$="x86 System") and ($sw_arch$="64")
	LogError "Cannot install 64 bit version of " + $ProductName$ + " on a 32 bit operating system!"
	isFatalError
endif

;-----------------------------------------------------------
;*** property: language - "de_DE, "en_US"
; application language defined via environment variable LANG
;-----------------------------------------------------------
; ...not yet implemented
; Set $sw_lang$ 				= GetProductProperty("default_language","en_US")

;-----------------------------------------------------------
;*** other properties:
; 		* set as default mail handler?
;-----------------------------------------------------------
; ...not yet implemented
; set $pr_set_default$		= GetProductProperty("set_default","False")

;-----------------------------------------------------------
;*** property: create desktop/startmenu/quickstart link?
;-----------------------------------------------------------
Set $pr_DesktopLink$ 		= GetProductProperty("link_desktop","False")

;-----------------------------------------------------------
;*** properties: post install/uninstall
;-----------------------------------------------------------
set $CustomPostInstall$ 	= getProductProperty("custom_post_install","none")
set $CustomPostUninstall$ 	= getProductProperty("custom_post_uninstall","none")

;-----------------------------------------------------------
;*** misc settings
;-----------------------------------------------------------
setlogLevel 				= $LogLvl$
set $PSDETAIL$ 				= "%ScriptPath%\bin\psdetail.exe"
set $PSDETAIL_mod$			= ""
set $Kill_Running$ 			= GetProductProperty("kill_running", "False")

Set $RegPathUninstall64$ 	= "HKEY_LOCAL_MACHINE\SOFTWARE\Microsoft\Windows\CurrentVersion\Uninstall"
Set $RegPathUninstall32$ 	= "HKEY_LOCAL_MACHINE\SOFTWARE\WOW6432Node\Microsoft\Windows\CurrentVersion\Uninstall"

;-----------------------------------------------------------
;*** specific settings
;-----------------------------------------------------------
Set $InstallerType$   	= "NSIS"
include_append "%ScriptPath%\exitcode_nsis.opsiinc"


; packages (for example):
; 		claws-mail-3.16.0-1-32bit.exe
; 		claws-mail-3.16.0-1-64bit.exe
Set $Installer$		  	= "%ScriptPath%\files\" + $StrippedProductId$ + "-{{O_SOFTWARE_VER}}-{{O_SOFTWARE_BUILD}}-" + $sw_arch$ + "bit"+ ".exe"
Set $InstallDir$      	= $InstallBase$ + "\" + stringReplace($ProductName$," ", "") + "\"
;Set $InstallDir$      	= $InstallBase$ + "\" + $ProductName$ + "\"
Set $MinimumSpace$    	= "140 MB"
Set $CheckBin$       	= $StrippedProductId$ + ".exe"
Set $Manual$			= $StrippedProductId$ + "-manual.pdf"
Set $SearchPattern$ 	= $ProductName$
Set $Uninst_ID_std$	   	= "ClawsMail"
Set $Uninst_Prg$       	= $StrippedProductId$ + "-uninstall.exe"
Set $Uninstaller$	   	= $InstallDir$ + $Uninst_Prg$
; ...oder UninstallString aus Registry

Set $Inst_Opts$        	= "/S  /D=" + $InstallDir$
Set $Uninst_Opts$      	= "/S"
